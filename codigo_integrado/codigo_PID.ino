//CODE BY TURMINHA DO DIDI üòÇü§£

#include <QTRSensors.h> 

// MOTORES
const int motorA1 = 3, motorA2 = 2, motorB1 = 4, motorB2 = 5; // Define os pinos de controle dos motores A e B
const int speedMax = 200; // Define a velocidade m√°xima permitida para ambos os motores (0 a 255)
const int baseSpeed = 160; // Define a velocidade base dos motores, que ser√° ajustada pelo controle PID

// PID
int lastError = 0; // Armazena o √∫ltimo erro calculado, usado para o termo derivativo do PID
float Kp = 55, Kd = 30, Ki = 0.00; // Constantes de ajuste do PID (Proporcional, Derivativo, Integral)
int proportional, integral, derivative; // Vari√°veis PID

//SENSORES
QTRSensors qtr; // Cria um objeto da classe QTRSensors para gerenciar os sensores de reflet√¢ncia
const uint8_t SensorCount = 5; // Define o n√∫mero de sensores de reflet√¢ncia usados
uint16_t sensorValues[SensorCount]; // Array para armazenar os valores lidos pelos sensores

//SETUP
void setup() {
  qtr.setTypeRC();  // Configura os sensores como tipo RC (resistor-capacitor)
  qtr.setSensorPins((const uint8_t[]){12,11,10,9,8}, SensorCount); // Define os pinos dos sensores de reflet√¢ncia

  // Configura os pinos dos motores como sa√≠da
  pinMode(motorA1, OUTPUT); 
  pinMode(motorA2, OUTPUT);
  pinMode(motorB1, OUTPUT); 
  pinMode(motorB2, OUTPUT);
  
  calibrateSensors(); // Realiza a calibra√ß√£o dos sensores de reflet√¢ncia
  delay(5000); // Pausa por 5 segundos para dar tempo de ajuste/calibra√ß√£o 
}

//CALIBRAGEM
void calibrateSensors() {
  digitalWrite(LED_BUILTIN, HIGH);   // Acende o LED embutido para indicar o in√≠cio da calibra√ß√£o
  for (uint16_t i = 0; i < 50; i++) qtr.calibrate(); // Realiza a calibra√ß√£o dos sensores (captura valores m√°ximos e m√≠nimos)
  digitalWrite(LED_BUILTIN, LOW); // Apaga o LED embutido ap√≥s a calibra√ß√£o
}

void loop() {
  int error = 2000 - qtr.readLineBlack(sensorValues); // Calcula o erro, que √© a diferen√ßa entre a posi√ß√£o desejada (2000) e a posi√ß√£o lida pelos sensores
  proportional = error; // O termo Proporcional √© simplesmente o erro atual (erro * t)
  integral += error; // O termo Integral acumula o erro ao longo do tempo (Ki‚à´e dt)
  derivative = error - lastError; // O termo Derivativo √© a diferen√ßa entre o erro atual e o erro anterior (Kd(Œîe/Œît) ou de/dt)
  lastError = error; // Atualiza o √∫ltimo erro para o pr√≥ximo c√°lculo do Derivativo
  
  // S = ‚àë((Kp(et) + Ki‚à´e dt + Kd(de/dt))
  int PIDValue = (Kp * proportional) + (Ki * integral) + (Kd * derivative); // Calcula o valor de corre√ß√£o da velocidade usando o PID
  bool noventa_graus = (sensorValues[0] < 100 || sensorValues[4] < 100); // 90 graus

  //Limita velocidade com base na posi√ß√£o
  int speedA, speedB;
    if (noventa_graus) {
      // Ajusta as velocidades para uma curva
      speedA = constrain(baseSpeed - PIDValue, 0, speedMax); // Motor interno da curva mais lento
      speedB = constrain(baseSpeed + PIDValue, 0, speedMax); // Motor externo da curva mais r√°pido
    } else {
      // Controle normal de linha reta
      speedA = constrain(baseSpeed + PIDValue, 0, speedMax);
      speedB = constrain(baseSpeed - PIDValue, 0, speedMax);
    }

  driveMotors(speedA, speedB); // Envia as velocidades ajustadas para os motores para que o rob√¥ siga a linha
}

void driveMotors(int posa, int posb) {
  analogWrite(motorA1, LOW); 
  analogWrite(motorA2, posa);
  analogWrite(motorB1, LOW); 
  analogWrite(motorB2, posb);   // Controla a dire√ß√£o e a velocidade dos motores A e B
}
